/*
 * Intel Edison
 *
 * This adds FocalTech FT6236 TouchScreen to the I2C6 bus available
 * on Intel Edison/Arduino header.
 *
 * In Linux you need to set CONFIG_TOUCHSCREEN_EDT_FT5X06=y (or m)
 * to be able to use this device.
 *
 * Include this file from an ASL providing SSDT DefinitionBlock.
 */
External (\_SB.PCI0.I2C6, DeviceObj)

Scope (\_SB.PCI0.I2C6)
{
    Device (TCH0) {
        Name (_HID, "PRP0001")
        Name (_DDN, "EDT FT5x06 I2C Touchscreen")
        Name (_CRS, ResourceTemplate () {
            I2cSerialBus (
                0x38,                   // Bus address
                ControllerInitiated,    // Don't care
                400000,                 // Fast mode (400 kHz)
                AddressingMode7Bit,     // 7-bit addressing
                "\\_SB.PCI0.I2C6",      // I2C host controller
                0                       // Must be 0
            )
            GpioInt(Level, ActiveLow, Exclusive, PullNone, 0,
                "\\_SB.PCI0.GPIO", 0, ResourceConsumer, , ) { 48 }
        })

        /*
         * See Documentation/devicetree/bindings/input/touchscreen/edt-ft5x06.txt
         * for more information about these bindings.
         */
        Name (_DSD, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () { "compatible", Package () { "focaltech,ft6236" } },
            }
        })
    }
}
