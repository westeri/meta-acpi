/*
 * Intel Edison
 *
 * This adds SSD1331 96x64 display device, 0.95inch, 65535 color OLED.
 *
 * Arduino pin		OLED pin
 * -----------		-------
 *	IO7		RES
 *	IO8		DC
 *	IO10		CS
 *	IO11		SDA
 *	IO13		SCL
 *	5V		VCC
 *	GND		GND
 *
 * In Linux you need to set CONFIG_FB_TFT_SSD1331=y (or m) to be able to use
 * this device.
 *
 * Include this file from an ASL providing SSDT DefinitionBlock.
 */

External (\_SB.PCI0.SPI5, DeviceObj)

Scope (\_SB.PCI0.SPI5)
{
    Device (CLED) {
        Name (_HID, "PRP0001")
        Name (_DDN, "SSD1331 65k color OLED")
        Name (_CRS, ResourceTemplate () {
            SpiSerialBus (
                1,                      // Chip select
                PolarityLow,            // Chip select is active low
                FourWireMode,           // Full duplex
                8,                      // Bits per word is 8 (byte)
                ControllerInitiated,    // Don't care
                8000000,                // 8 MHz
                ClockPolarityLow,       // SPI mode 0
                ClockPhaseFirst,        // SPI mode 0
                "\\_SB.PCI0.SPI5",      // SPI host controller
                0                       // Must be 0
            )
            GpioIo(Exclusive, PullNone, 0, 0, IoRestrictionOutputOnly,
                "\\_SB.PCI0.GPIO", 0, ResourceConsumer, , ) { 49 }
            GpioIo(Exclusive, PullUp, 0, 0, IoRestrictionOutputOnly,
                "\\_SB.PCI0.GPIO", 0, ResourceConsumer, , ) { 48 }
        })

        /*
         * See drivers/staging/fbtft/fbtft-core.c::fbtft_properties_read()
         * for more information about these bindings.
         */
        Name (_DSD, Package () {
            ToUUID("daffd814-6eba-4d8c-8a91-bc9bbf4aa301"),
            Package () {
                Package () { "compatible", Package () { "solomon,ssd1331" } },
                Package () { "dc-gpios", Package () { ^CLED, 0, 0, 0 } },
                Package () { "reset-gpios", Package () { ^CLED, 1, 0, 1 } },
                Package () { "width", 96 },
                Package () { "height", 64 },
                Package () { "buswidth", 8 },
            }
        })
    }
}
